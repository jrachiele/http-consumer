subprojects {
    apply plugin: 'java'
    group = 'com.github.jrachiele'
    version = '0.1'
    repositories {
        mavenCentral()
    }
    task generateJavadoc(type: Javadoc) {
        source = sourceSets.main.allJava
        classpath = sourceSets.main.compileClasspath
        options.memberLevel = JavadocMemberLevel.PUBLIC
        options {
            links "https://docs.oracle.com/javase/8/docs/api/"
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from generateJavadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.25'
        implementation 'org.apache.httpcomponents:fluent-hc:4.5.3'
        compileOnly 'org.projectlombok:lombok:1.16.14'

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.hamcrest:hamcrest-all:1.3'
        testImplementation 'org.mockito:mockito-core:2.7.11'
        testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.25'
    }
}

project(':http-consumer-core') {

}

project(':capmetro-http-consumer') {
    dependencies {
        compile project(':http-consumer-core')
    }
}

